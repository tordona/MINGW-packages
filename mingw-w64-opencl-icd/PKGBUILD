# Maintainer: Peter Budai <peterbudai at hotmail.com>

_realname=opencl-icd
_commonname="OpenCL-ICD-Loader"
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2024.10.24
pkgrel=1
pkgdesc="OpenCL ICD Loader (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/KhronosGroup/OpenCL-ICD-Loader'
msys2_references=(
  'aur: mingw-w64-opencl-icd'
)
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-opencl-headers"
             "${MINGW_PACKAGE_PREFIX}-tools")
source=("https://github.com/KhronosGroup/OpenCL-ICD-Loader/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-do-not-use-mingw32-def-on-clang32.patch")
sha256sums=('95f2f0cda375b13d2760290df044ebea9c6ff954a7d7faa0867422442c9174dc'
            'eb947c742dd31f2d1cc18ec8eb9af686385cc8634a9c56a3c4fb36d530d8280f')

prepare() {
  cd "${srcdir}"/OpenCL-ICD-Loader-${pkgver}
  patch -p1 -i "${srcdir}"/001-do-not-use-mingw32-def-on-clang32.patch
}

build() {
  common_config=(
    -G'Ninja'
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}
    -DCMAKE_BUILD_TYPE=Release
    -DOPENCL_ICD_LOADER_HEADERS_DIR=${MINGW_PREFIX}/include
    -DOPENCL_ICD_LOADER_BUILD_TESTING=OFF
    -DBUILD_TESTING=OFF
  )

  msg2 "Building shared library"
  mkdir -p "${srcdir}"/build-${MSYSTEM}-shared && cd "${srcdir}"/build-${MSYSTEM}-shared

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      "${common_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DOPENCL_ICD_LOADER_BUILD_SHARED_LIBS=ON \
      ../${_commonname}-${pkgver}

  cmake --build .
  
  msg2 "Building static library"
  mkdir -p "${srcdir}"/build-${MSYSTEM}-static && cd "${srcdir}"/build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      "${common_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DOPENCL_ICD_LOADER_BUILD_SHARED_LIBS=OFF \
      ../${_commonname}-${pkgver}

  cmake --build .
}

package() {
  msg2 "Installing shared library"
  cd "${srcdir}"/build-${MSYSTEM}-shared
  DESTDIR="${pkgdir}" cmake --install .
  
  msg2 "Installing static library"
  cd "${srcdir}"/build-${MSYSTEM}-static
  DESTDIR="${pkgdir}" cmake --install .

  mv "${pkgdir}"${MINGW_PREFIX}/bin/OpenCL.dll "${pkgdir}"${MINGW_PREFIX}/bin/libOpenCL.dll
  mv "${pkgdir}"${MINGW_PREFIX}/lib/OpenCL.a "${pkgdir}"${MINGW_PREFIX}/lib/libOpenCL.a

  echo "Libs.private: -lole32 -lshlwapi -lcfgmgr32" >> "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/OpenCL.pc

  install -Dm644 "${srcdir}"/${_commonname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
