# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=harfbuzz
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         $([[ ${CARCH} == i686 ]] || echo \
           "${MINGW_PACKAGE_PREFIX}-${_realname}-cairo" \
           "${MINGW_PACKAGE_PREFIX}-${_realname}-icu" \
           "${MINGW_PACKAGE_PREFIX}-${_realname}-utils" \
           "${MINGW_PACKAGE_PREFIX}-${_realname}-docs"))
pkgver=12.3.0
pkgrel=1
pkgdesc="OpenType text shaping engine"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://harfbuzz.github.io/'
msys2_repository_url='https://github.com/harfbuzz/harfbuzz/'
msys2_references=(
  "archlinux: harfbuzz"
  "cpe: cpe:/a:harfbuzz_project:harfbuzz"
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-graphite2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             $([[ ${CARCH} == i686 ]] || echo \
               "${MINGW_PACKAGE_PREFIX}-gtk-doc" \
               "${MINGW_PACKAGE_PREFIX}-icu" \
               "${MINGW_PACKAGE_PREFIX}-cairo" \
               "${MINGW_PACKAGE_PREFIX}-chafa")
             "${MINGW_PACKAGE_PREFIX}-ragel")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-fonttools"
              "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/harfbuzz-${pkgver}.tar.xz"
        "001-fix-build-with-chafa.patch"
        "harfbuzz.pc.patch")
sha256sums=('8660ebd3c27d9407fc8433b5d172bafba5f0317cb0bb4339f28e5370c93d42b7'
            '26b37a1ca9872973905ecb96bcbe3f054472252320956faa74428206900d360e'
            '47f51854e6f70b56dad94980bd22db9a6d592f4932f5d897f39f0d4c54b98d39')
noextract=("harfbuzz-${pkgver}.tar.xz")

prepare() {
  echo "Extracting harfbuzz-${pkgver}.tar.xz..."
  tar -xJf harfbuzz-${pkgver}.tar.xz || true

  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-with-chafa.patch
}

build() {
  local -a _static_flags=(
    -DGIO_STATIC_COMPILATION
    -DGLIB_STATIC_COMPILATION
    -DGMODULE_STATIC_COMPILATION
    -DGOBJECT_STATIC_COMPILATION
    -DGRAPHITE2_STATIC
    -DCAIRO_WIN32_STATIC_BUILD
  )

  declare -a _extra_config
  if [[ ${CARCH} != "i686" ]]; then
    _extra_config+=(
      "-Dchafa=enabled"
      "-Dcairo=enabled"
      "-Dicu=enabled"
      "-Ddocs=enabled"
      "-Ddoc_tests=false"
      "-Dutilities=enabled"
      "-Dintrospection=disabled"
      "-Dgdi=enabled"
      "-Dtests=disabled"
      "-Dgobject=enabled"
      "-Ddirectwrite=enabled"
    )
  else
    _extra_config+=(
      "-Dchafa=disabled"
      "-Dcairo=disabled"
      "-Dglib=disabled"
      "-Dicu=disabled"
      "-Ddocs=disabled"
      "-Ddoc_tests=false"
      "-Dutilities=disabled"
      "-Dintrospection=disabled"
      "-Dgdi=enabled"
      "-Dtests=disabled"
      "-Dgobject=enabled"
      "-Ddirectwrite=enabled"
    )
  fi

  CFLAGS+=" ${_static_flags[@]}" \
  CXXFLAGS+=" ${_static_flags[@]}" \
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson setup \
    "${_extra_config[@]}" \
    --prefix="${MINGW_PREFIX}" \
    --buildtype plain \
    --wrap-mode=nofallback \
    --default-library=static \
    -Dauto_features=enabled \
    -Dfreetype=enabled \
    -Dgraphite=enabled \
    "build-${MSYSTEM}-static" \
    "${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson compile -C "build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson setup \
    "${_extra_config[@]}" \
    --prefix="${MINGW_PREFIX}" \
    --buildtype plain \
    --wrap-mode=nofallback \
    --default-library=shared \
    -Dauto_features=enabled \
    -Dfreetype=disabled \
    -Dgraphite2=enabled \
    "build-${MSYSTEM}-shared" \
    "${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson compile -C "build-${MSYSTEM}-shared"
}

check(){
  cd "${srcdir}/build-${MSYSTEM}-shared"
  ${MINGW_PREFIX}/bin/meson setup -Dtests=enabled ../${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/meson compile
  ${MINGW_PREFIX}/bin/meson test
}

package_harfbuzz() {
  patch -Np0 -l -i "${srcdir}"/harfbuzz.pc.patch
  
  pkgdesc+=" (mingw-w64)"

  ${MINGW_PREFIX}/bin/meson install -C "build-${MSYSTEM}-shared" --destdir "${pkgdir}"

  ${MINGW_PREFIX}/bin/meson install -C "build-${MSYSTEM}-static" --destdir "${pkgdir}"

  if [[ ${CARCH} != "i686" ]]; then

  mkdir -p dest-docs${MINGW_PREFIX}/share
  mv "${pkgdir}"${MINGW_PREFIX}/share/gtk-doc dest-docs${MINGW_PREFIX}/share/gtk-doc

  mkdir -p dest-utils${MINGW_PREFIX}/bin
  mv "${pkgdir}"${MINGW_PREFIX}/bin/*.exe dest-utils${MINGW_PREFIX}/bin/

  mkdir -p dest-cairo${MINGW_PREFIX}/bin
  mkdir -p dest-cairo${MINGW_PREFIX}/include/harfbuzz/
  mkdir -p dest-cairo${MINGW_PREFIX}/lib/pkgconfig
  mv "${pkgdir}"${MINGW_PREFIX}/bin/libharfbuzz-cairo*.dll dest-cairo${MINGW_PREFIX}/bin/
  mv "${pkgdir}"${MINGW_PREFIX}/include/harfbuzz/hb-cairo.h dest-cairo${MINGW_PREFIX}/include/harfbuzz/
  mv "${pkgdir}"${MINGW_PREFIX}/lib/libharfbuzz-cairo*.a dest-cairo${MINGW_PREFIX}/lib/
  mv "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/harfbuzz-cairo.pc dest-cairo${MINGW_PREFIX}/lib/pkgconfig/

  mkdir -p dest-icu${MINGW_PREFIX}/bin
  mkdir -p dest-icu${MINGW_PREFIX}/include/harfbuzz/
  mkdir -p dest-icu${MINGW_PREFIX}/lib/pkgconfig
  mv "${pkgdir}"${MINGW_PREFIX}/bin/libharfbuzz-icu*.dll dest-icu${MINGW_PREFIX}/bin/
  mv "${pkgdir}"${MINGW_PREFIX}/include/harfbuzz/hb-icu.h dest-icu${MINGW_PREFIX}/include/harfbuzz/
  mv "${pkgdir}"${MINGW_PREFIX}/lib/libharfbuzz-icu*.a dest-icu${MINGW_PREFIX}/lib/
  mv "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/harfbuzz-icu.pc dest-icu${MINGW_PREFIX}/lib/pkgconfig/

  fi

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}

package_harfbuzz-cairo() {
  if [ -d "dest-cairo" ]; then
    pkgdesc+=" - Cairo integration (mingw-w64)"
    depends=("${MINGW_PACKAGE_PREFIX}-harfbuzz"
             "${MINGW_PACKAGE_PREFIX}-cairo")

    mv dest-cairo/* "${pkgdir}"
  fi
}

package_harfbuzz-icu() {
  if [ -d "dest-icu" ]; then
    pkgdesc+=" - ICU integration (mingw-w64)"
    depends=("${MINGW_PACKAGE_PREFIX}-harfbuzz"
             "${MINGW_PACKAGE_PREFIX}-icu")

    mv dest-icu/* "${pkgdir}"
  fi
}

package_harfbuzz-utils() {
  if [ -d "dest-utils" ]; then
    pkgdesc+=" - Utilities (mingw-w64)"
    depends=("${MINGW_PACKAGE_PREFIX}-harfbuzz"
             "${MINGW_PACKAGE_PREFIX}-harfbuzz-cairo"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             $([[ ${CARCH} == i686 ]] || echo "${MINGW_PACKAGE_PREFIX}-chafa"))

    mv dest-utils/* "${pkgdir}"
  fi
}

package_harfbuzz-docs() {
  if [ -d "dest-docs" ]; then
    pkgdesc+=" - Documentation (mingw-w64)"
    depends=()
    optdepends=()

    mv dest-docs/* "${pkgdir}"
  fi
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
