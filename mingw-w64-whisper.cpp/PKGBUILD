# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=whisper.cpp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
epoch=1
pkgver=1.8.2.r406.ge9898ddf
pkgrel=1
pkgdesc="Port of OpenAI's Whisper model in C/C++ (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url='https://github.com/ggml-org/whisper.cpp'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-ggml"
  "${MINGW_PACKAGE_PREFIX}-SDL2"
  "${MINGW_PACKAGE_PREFIX}-shaderc"
  "${MINGW_PACKAGE_PREFIX}-vulkan-headers"
  "${MINGW_PACKAGE_PREFIX}-opencl-headers"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  #"git"
)
_commit=e9898ddfb908ffaa7026c66852a023889a5a7202
source=("git+https://github.com/ggml-org/whisper.cpp#commit=${_commit}"
#source=("https://github.com/ggml-org/whisper.cpp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "whisper.pc.in.patch")
sha256sums=('9c99231c8e450a48bc9e1baa4b3df884870b935db17eee5c591c45cc17134ff0'
            '96efa2431d71114f5928ab04c6d048a8814f5dd54713914e8e98adcac25ac844')

pkgver() {
  cd "${srcdir}"/${_realname}-${pkgver}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  if [[ -v _commit ]]; then
    mv "${srcdir}"/${_realname} "${srcdir}"/${_realname}-${pkgver}
  fi

  sed -i "s/@PACKAGE@/${_realname}-${pkgver}/g" "whisper.pc.in.patch"

  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/whisper.pc.in.patch
}

build() {
  [[ ${CARCH} == "aarch64" ]] && cpu_feature="OFF" || cpu_feature="ON"
 
  common_config=(
    -GNinja
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
    -DCMAKE_BUILD_TYPE=Release
    -DWHISPER_USE_SYSTEM_GGML=OFF
    -DWHISPER_CURL=ON
    -DWHISPER_SDL2=ON
    -DWHISPER_BUILD_EXAMPLES=OFF
    -DWHISPER_BUILD_TESTS=OFF
    -DWHISPER_BUILD_SERVER=OFF
    -DWHISPER_ALL_WARNINGS=OFF
    -DGGML_OPENCL=ON
    -DGGML_VULKAN=ON
    -DGGML_CCACHE=OFF
    -DGGML_NATIVE=OFF
    -DGGML_SSE42=$cpu_feature
    -DGGML_AVX=$cpu_feature
    -DGGML_F16C=$cpu_feature
    -DGGML_AVX2=$cpu_feature
    -DGGML_BMI2=$cpu_feature
    -DGGML_FMA=$cpu_feature
  )

  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  LDFLAGS+=" -Wl,--export-all-symbols" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      "${common_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      ../${_realname}-${pkgver}

  cmake --build .

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"

  LDFLAGS+=" -Wl,--export-all-symbols" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      "${common_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      ../${_realname}-${pkgver}

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-shared"

  DESTDIR="${pkgdir}" cmake --install .

  for ggml in "${pkgdir}${MINGW_PREFIX}"/bin/ggml*.dll; do
     mv "${ggml}" "$(dirname "${ggml}")/lib$(basename "${ggml}")"
  done

  cd "${srcdir}/build-${MSYSTEM}-static"

  DESTDIR="${pkgdir}" cmake --install .

  # Rename output files by prefixing them with whisper (if not already prefixed and if not DLL)
  for i in "${pkgdir}"${MINGW_PREFIX}/bin/*.{exe,py,sh}; do
    filename=$(basename "$i")
    
    if [[ -e $i && ! ("$filename" =~ ^whisper) ]]; then
      mv "${i}" "${pkgdir}${MINGW_PREFIX}/bin/whisper-${filename}"
    fi
  done

  for ggml in "${pkgdir}${MINGW_PREFIX}"/lib/ggml*.a; do
    mv "${ggml}" "$(dirname "${ggml}")/lib$(basename "${ggml}")"
  done

  install -Dm755 "${srcdir}/${_realname}-${pkgver}/models/download-ggml-model.sh" \
    "${pkgdir}${MINGW_PREFIX}/bin/whisper-download-ggml-model.sh"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
