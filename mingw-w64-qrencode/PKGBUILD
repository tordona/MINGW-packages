# Contributor: Jevgeny Krasovsky <jkrasovsky@gmail.com>

_realname=qrencode
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.1.1
pkgrel=4
pkgdesc="C library for encoding data in a QR Code symbol (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://fukuchi.org/works/qrencode/index.en.html"
msys2_repository_url="https://github.com/fukuchi/libqrencode"
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-SDL")
source=(https://github.com/fukuchi/libqrencode/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('5385bc1b8c2f20f3b91d258bf8ccc8cf62023935df2d2676b5b67049f31a049c')

prepare() {
  cd "${srcdir}"/lib${_realname}-${pkgver}
}

build() {
  common_config=(
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -WITH_TOOLS=NO \
    -WITH_TESTS=NO \
    -WITHOUT_PNG=NO \
    -DGPROF=OFF \
    -DCOVERAGE=OFF \
    -DASAN=OFF \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  )

  msg "Build shared version"
  mkdir -p "${srcdir}/build-shared-${MSYSTEM}" && cd "${srcdir}/build-shared-${MSYSTEM}"  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    "${common_config[@]}" \
    ../lib${_realname}-${pkgver}

  cmake --build ./

  msg "Build static version"
  mkdir -p "${srcdir}/build-static-${MSYSTEM}" && cd "${srcdir}/build-static-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=off \
    "${common_config[@]}" \
    ../lib${_realname}-${pkgver}

  cmake --build ./
}

package() {
  cd "${srcdir}/build-shared-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  cd "${srcdir}/build-static-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/lib${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
