# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=codec2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2.0
pkgrel=1
pkgdesc="Open source speech codec designed for communications quality speech between 450 and 3200 bit/s (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/drowe67/codec2'
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("$url/archive/$pkgver/$_realname-$pkgver.tar.gz"
        0001-GetDependencies_point_to_correct_file.patch)
sha256sums=('cbccae52b2c2ecc5d2757e407da567eb681241ff8dadce39d779a7219dbcf449'
            '2379f3dfc592c27111cfab1e52f6d4fd73341f39819eac119a5fed367ec87e50')
options=('libtool' 'staticlibs')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}"/0001-GetDependencies_point_to_correct_file.patch
}

build() {
  msg2 "Building shared library"
  mkdir -p "${srcdir}"/build-${MSYSTEM}-shared && cd "${srcdir}"/build-${MSYSTEM}-shared

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_SKIP_RPATH=YES \
      -DBUILD_SHARED_LIBS=ON \
      -DUNITTEST=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
  
  msg2 "Building static library"
  mkdir -p "${srcdir}"/build-${MSYSTEM}-static && cd "${srcdir}"/build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_SKIP_RPATH=YES \
      -DBUILD_SHARED_LIBS=OFF \
      -DUNITTEST=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}-shared
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  cd "${srcdir}"/build-${MSYSTEM}-static
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  # prevent duplicate symbols with libspeex
  objcopy --redefine-sym lpc_to_lsp=libcodec2_lpc_to_lsp "${pkgdir}"/${MSYSTEM}/lib/libcodec2.a
  objcopy --redefine-sym lsp_to_lpc=libcodec2_lsp_to_lpc "${pkgdir}"/${MSYSTEM}/lib/libcodec2.a

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
