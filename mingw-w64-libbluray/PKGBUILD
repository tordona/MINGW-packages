# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libbluray
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.0
pkgrel=2
pkgdesc="Library to access Blu-Ray disks for video playback (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.videolan.org/developers/libbluray.html'
msys2_repository_url='https://code.videolan.org/videolan/libbluray'
msys2_references=(
  "cpe: cpe:/a:videolan:libbluray"
)
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-freetype")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-cc")
source=(https://download.videolan.org/pub/videolan/libbluray/${pkgver}/${_realname}-${pkgver}.tar.xz
        "meson-build.patch")
sha256sums=('77937baf07eadda4b2b311cf3af4c50269d2ea3165041f5843d96476c4c92777'
            '7ddfe27b50bf5c3e68c8433beb43323397a01ce1c0274df07be2ee03ee4319b5')
options=('libtool' 'staticlibs')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/meson-build.patch"
}

build() {
common_config=(
  --prefix="${MINGW_PREFIX}"
  -Denable_docs=false
  -Denable_tools=false
  -Denable_devtools=false
  -Denable_examples=false
  -Dbdj_jar=disabled
  -Dfontconfig=enabled
  -Dfreetype=enabled
  -Dlibxml2=enabled
)
		
  export CPPFLAGS="-Ddec_init=libbluray_dec_init"
  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
  "${common_config[@]}" \
  -Ddefault_library=shared \
  "../${_realname}-${pkgver}"

  ninja -j 8

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
  "${common_config[@]}" \
  -Dc_args="-DLIBXML_STATIC" \
  -Ddefault_library=static \
  "../${_realname}-${pkgver}"

  ninja -j 8
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" meson install

  # prevent duplicate symbols
  objcopy --redefine-sym dec_init=libbluray_dec_init "${pkgdir}"/${MSYSTEM}/lib/libbluray.a
  
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
