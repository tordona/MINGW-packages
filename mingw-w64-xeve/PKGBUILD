# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=xeve
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.1
pkgrel=2
pkgdesc="MPEG-5 EVC (Essential Video Coding) encoder (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/mpeg5/xeve/'
license=('spdx:BSD-3-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             $([[ ${CARCH} == aarch64 ]] && echo "${MINGW_PACKAGE_PREFIX}-sse2neon"))
source=("https://github.com/mpeg5/xeve/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "010-xeve-disable-werror.patch"
        "002-sys-sse2neon.patch"
        "xeve.pc.in.patch")
sha256sums=('238c95ddd1a63105913d9354045eb329ad9002903a407b5cf1ab16bad324c245'
            '8c4b607f34a5d39e824f86d00ab101849595cb49a2f67eed131487d658ec7206'
            'e297b65a9d7bc251c9944c28ba389716e3a345a0ef71795a47030911d74b360f'
            'f649af90d6f3e1c2d6d3e47c4401ba240ca682cd6aa601e086e7bf5c73050f48')

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  echo "v${pkgver}" > version.txt

  apply_patch_with_msg \
    010-xeve-disable-werror.patch \
    002-sys-sse2neon.patch \
    xeve.pc.in.patch
}

build() {
  common_config=(
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}
    -DCMAKE_BUILD_TYPE=Release
  )

  if [[ "${CARCH}" == "aarch64" ]]; then
    common_config+=("-DARM=TRUE")
  fi

  msg "Build shared version"
  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared" 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DBUILD_SHARED_LIBS=ON \
    "${common_config[@]}" \
    ../${_realname}-${pkgver}

  cmake --build ./

  msg "Build static version"
  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static" 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DBUILD_SHARED_LIBS=OFF \
    "${common_config[@]}" \
    ../${_realname}-${pkgver}

 cmake --build ./
}

package() {
  # Install shared
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Install static
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
