# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=xevd
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.0
pkgrel=2
pkgdesc="MPEG-5 EVC (Essential Video Coding) decoder (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/mpeg5/xevd/'
license=('spdx:BSD-3-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/mpeg5/xevd/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "010-xevd-disable-werror.patch"
        "xevd.pc.in.patch")
sha256sums=('8d55c7ec1a9ad4e70fe91fbe129a1d4dd288bce766f466cba07a29452b3cecd8'
            '2a7eff2690c0d4d441df97ad37fd7a0e3e0a03705665dad12201f8d8d997f191'
            'fd5d66eba622ea839baecea55057fac8e8d72241b6aae541aaa462386fd5a16f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  echo "v${pkgver}" > version.txt

  patch -Np1 -i "${srcdir}"/010-xevd-disable-werror.patch
  patch -Np0 -i "${srcdir}"/xevd.pc.in.patch
}

build() {
  common_config=(
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}
    -DCMAKE_BUILD_TYPE=Release
  )

  if [[ "${CARCH}" == "aarch64" ]]; then
    common_config+=("-DARM=TRUE")
  fi

  msg "Build shared version"
  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared" 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DBUILD_SHARED_LIBS=ON \
    -DXEVD_APP_STATIC_BUILD=OFF \
    "${common_config[@]}" \
    ../${_realname}-${pkgver}

  cmake --build ./

  msg "Build static version"
  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static" 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DBUILD_SHARED_LIBS=OFF \
    -DXEVD_APP_STATIC_BUILD=ON \
    "${common_config[@]}" \
    ../${_realname}-${pkgver}

  cmake --build ./
}

package() {
  # Install shared
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Install static
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
