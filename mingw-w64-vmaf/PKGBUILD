# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=vmaf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.0
pkgrel=2
pkgdesc='Perceptual video quality assessment algorithm based on multi-method fusion (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/Netflix/vmaf/'
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("https://github.com/Netflix/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "libvmaf.pc.patch")
sha256sums=('7178c4833639e6b989ecae73131d02f70735fdb3fc2c7d84bc36c9c3461d93b1'
'32106ffb14b3ede5cc68ed8c8ca70bdf7c3eeb1386dc185f9ab983b2729e2f58')

build() {
  common_config=(
    --wrap-mode=nodownload
    --auto-features=enabled
	--buildtype=release
	-Dbuilt_in_models=true
	-Denable_tests=false
	-Denable_docs=false
	-Denable_asm=true
	-Denable_avx512=true
	-Denable_float=true
	-Denable_cuda=false
	-Denable_nvtx=false
  )
  
  mkdir -p "${srcdir}/build-shared-${MSYSTEM}" && cd "${srcdir}/build-shared-${MSYSTEM}"
  
  LDFLAGS+=" -lpthread" \
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
    --prefix="${MINGW_PREFIX}" \
	--default-library=shared \
    "${common_config[@]}" \
    "../${_realname}-${pkgver}/libvmaf"

  meson compile
  
  mkdir -p "${srcdir}/build-static-${MSYSTEM}" && cd "${srcdir}/build-static-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
    --prefix="${MINGW_PREFIX}" \
	--default-library=static \
    "${common_config[@]}" \
    "../${_realname}-${pkgver}/libvmaf"

  meson compile
}

check() {
  cd "${srcdir}/build-shared-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/meson.exe test || true
  
  cd "${srcdir}/build-static-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/meson.exe test || true
}

package() {
  patch -Np0 -l -i "${srcdir}/libvmaf.pc.patch"
  
  cd "${srcdir}/build-shared-${MSYSTEM}"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/build-static-${MSYSTEM}"
  DESTDIR="${pkgdir}" meson install
   
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
