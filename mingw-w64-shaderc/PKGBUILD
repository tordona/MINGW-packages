# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=shaderc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2025.5
pkgrel=1
pkgdesc="Collection of tools, libraries and tests for shader compilation (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/google/shaderc"
msys2_references=(
  'archlinux: shaderc'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-glslang"
         "${MINGW_PACKAGE_PREFIX}-spirv-tools")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-spirv-headers"
             "${MINGW_PACKAGE_PREFIX}-asciidoctor")
source=("https://github.com/google/shaderc/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-fix-glslang-hlsl-linking-order.patch"
        "shaderc.pc.patch"
        "shaderc_combined.pc.patch"
        "shaderc_static.pc.patch")
sha256sums=('fca5041b1fdea6daba167b63e04e55e5059fab40828342126169336643445447'
            'af880b221603e2b5c34dd306feeb4fa9af5803ebc841d64ab8e4be0e7455a6ba'
            '8265c7f6ea0f6f317847e0b9ed2fbda9027551f3f1ff3b13a14d984b8db0381b'
            'f55282a68ee8b5ddeb60c9827d0fcffeef551c900274b4cf256c5537cb9b0e92'
            '35f15d8837506c40f168d72db340f75a0df278ed5920b19fa39e5270f0f252fb')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://www.mail-archive.com/devel@lists.fedoraproject.org/msg156020.html
  patch -Nbp1 -i "${srcdir}/0001-fix-glslang-hlsl-linking-order.patch"

  # de-vendor libs and disable git versioning
  sed '/examples/d;/third_party/d' -i CMakeLists.txt
  sed '/build-version/d' -i glslc/CMakeLists.txt
  cat <<- EOF > glslc/src/build-version.inc
"${pkgver}\\n"
"$(pacman -Q "${MINGW_PACKAGE_PREFIX}-spirv-tools"|cut -d \  -f 2|sed 's/-.*//')\\n"
"$(pacman -Q "${MINGW_PACKAGE_PREFIX}-glslang"|cut -d \  -f 2|sed 's/-.*//')\\n"
EOF
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  local -a _extra_config
  if check_option "debug" "y"; then
    _extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
  else
    _extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  local -a _glslang_inc
  _glslang_inc=$(cygpath -m "${MINGW_PREFIX}/include/glslang")

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${_extra_config[@]}" \
    -Dglslang_SOURCE_DIR=${_glslang_inc} \
    -DSHADERC_SKIP_TESTS=ON \
    -DSHADERC_SKIP_EXAMPLES=ON \
    -DSHADERC_ENABLE_WERROR_COMPILE=OFF \
    -DPython_EXECUTABLE="${MINGW_PREFIX}/bin/python.exe" \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./

  # Generate manpage
  # Suppress asciidoctor: ERROR: line 1: non-conforming manpage title
  cd "${srcdir}/${_realname}-${pkgver}/glslc"
  ${MINGW_PREFIX}/bin/asciidoctor -b manpage README.asciidoc -o glslc.1 || true
}

package() {
  sed -i "s/@BUILDPATH@/build-${MSYSTEM}/g" "${srcdir}/shaderc.pc.patch"
  sed -i "s/@BUILDPATH@/build-${MSYSTEM}/g" "${srcdir}/shaderc_combined.pc.patch"
  sed -i "s/@BUILDPATH@/build-${MSYSTEM}/g" "${srcdir}/shaderc_static.pc.patch"
  
  patch -Np0 -i "${srcdir}/shaderc.pc.patch"
  patch -Np0 -i "${srcdir}/shaderc_combined.pc.patch"
  patch -Np0 -i "${srcdir}/shaderc_static.pc.patch"
  
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  cp "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/shaderc.pc "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/shaderc_shared.pc
  cp "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/shaderc_static.pc "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/shaderc.pc

  cp ./libshaderc_util/libshaderc_util.a "${pkgdir}"${MINGW_PREFIX}/lib/
  cp ./glslc/libglslc.a "${pkgdir}"${MINGW_PREFIX}/lib/
  
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/glslc/glslc.1" \
  -t "${pkgdir}${MINGW_PREFIX}/share/man/man1" || true
}
