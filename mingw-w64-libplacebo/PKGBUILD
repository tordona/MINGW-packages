# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libplacebo
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.351.0
pkgrel=1
pkgdesc="Reusable library for GPU-accelerated video/image rendering primitives (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://code.videolan.org/videolan/libplacebo"
depends=("${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libdovi"
         "${MINGW_PACKAGE_PREFIX}-shaderc"
         "${MINGW_PACKAGE_PREFIX}-spirv-cross"
         "${MINGW_PACKAGE_PREFIX}-vulkan")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python-glad"
             "${MINGW_PACKAGE_PREFIX}-python-jinja"
             "${MINGW_PACKAGE_PREFIX}-fast_float"
             "${MINGW_PACKAGE_PREFIX}-xxhash"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers")
license=('LGPL2.1')
source=(${url}/-/archive/v${pkgver}/${_realname}-v${pkgver}.tar.gz
        "0002-utils_gen_py.patch")
sha256sums=('4efe1c8d4da3c61295eb5fdfa50e6037409d8425eb3c15dd86788679c4ce59ee'
            '7cb0cbe494c1345635ed1e07dad74fca4f55a6d57f00ce8ccd947b6c18aee56d')
use_libdovi=true

prepare() {
  sed -i "s/@PACKAGE@/${_realname}-v${pkgver}/g" "0002-utils_gen_py.patch"
  patch -p0 -i "${srcdir}"/0002-utils_gen_py.patch
}

build() {
  common_config=(
      --prefix="${MINGW_PREFIX}"
      --buildtype=release
      -Ddemos=false
      -Dtests=false
      -Dd3d11=enabled
      -Dlcms=enabled
      -Dvulkan=enabled
      -Dbench=false
      -Dfuzz=false
      -Dglslang=disabled
      -Dvk-proc-addr=disabled
      -Dvulkan-registry="${MINGW_PREFIX}"/share/vulkan/registry/vk.xml
  )
 
   if [[ "${use_libdovi}" == true ]]; then
    common_config+=(
      -Dlibdovi=enabled
    )
  else
    common_config+=(
      -Dlibdovi=disabled
    )
  fi
 
  mkdir -p "${srcdir}"/build-${MSYSTEM}-shared && cd "${srcdir}"/build-${MSYSTEM}-shared

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup                   \
      "${common_config[@]}"       \
      -Dshaderc=disabled          \
      --default-library=shared    \
      "../${_realname}-v${pkgver}"

  meson compile

  mkdir -p "${srcdir}"/build-${MSYSTEM}-static && cd "${srcdir}"/build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup                   \
      "${common_config[@]}"       \
      -Dshaderc=enabled           \
      --default-library=static    \
      "../${_realname}-v${pkgver}"

  meson compile
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}-static

  meson test
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}-shared
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}"/build-${MSYSTEM}-static
  DESTDIR="${pkgdir}" meson install

  sed -i "s/spirv\-cross\-c\-shared/spirv\-cross-c/g" "${pkgdir}/${MSYSTEM}"/lib/pkgconfig/${_realname}.pc
  sed -i "/\-lversion/aLibs.private: \-lstdc++" "${pkgdir}/${MSYSTEM}"/lib/pkgconfig/${_realname}.pc

  install -Dm644 "${srcdir}/${_realname}-v${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
