# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=aribb24
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.3
pkgrel=6
pkgdesc="A library for ARIB STD-B24, decoding JIS 8 bit characters and parsing MPEG-TS stream (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/nkoriyama/aribb24"
license=("spdx:LGPL-3.0-or-later")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
source=("https://github.com/nkoriyama/aribb24/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-no-undefined.patch"
        "configure.ac.patch")
sha256sums=('f61560738926e57f9173510389634d8c06cabedfa857db4b28fb7704707ff128'
            '2832363ed204051fbc34c34da0fbac993e7bc0213de3a34baf37b2622da87657'
            'a483ecb64cc8995129699f14c1238221148338ae51466de12724c7c87f435d9b')
options=('libtool' 'staticlibs')

prepare() {
  sed -i "s/@PACKAGE@/${_realname}-${pkgver}/g" "configure.ac.patch"

  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-no-undefined.patch
  patch -p1 -i "${srcdir}"/configure.ac.patch
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --with-pic

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make -j1 DESTDIR="${pkgdir}" install
}
